plugins {
  id 'application'
}

ext {
    scriptDefVmArgs = [ '-Xms64m', '-Xmx1g', '-XX:-OmitStackTraceInFastThrow' ]
}

application {
    mainClass = 'io.bdeploy.minion.cli.MinionServerCli'
}

dependencies {
    implementation(
        project(":common"),
        project(":logging"),
        project(":jersey"),
        project(":bhive"),
        project(":dcu"),
        project(":pcu"),
        project(":interfaces"),
        project(":ui"),
        project(":schema"),
        project(":messaging"),

        'org.bouncycastle:bcpkix-jdk18on:1.80',
        'org.bouncycastle:bcprov-jdk18on:1.80',
        'org.quartz-scheduler:quartz:2.5.0',
        'net.jsign:jsign-core:6.0',
        'com.nimbusds:oauth2-oidc-sdk:11.23.1'
    )

    commonTestDeps(project(":launcher"))
    commonTestDeps(testFixtures(project(":common")))
    commonTestDeps(testFixtures(project(":jersey")))
    commonTestDeps(testFixtures(project(":bhive")))
    commonTestDeps(testFixtures(project(":pcu")))
}

rootProject.addMultiPlatformDistBuild(project)

startScripts {
    applicationName = '_bdeploy'
}

evaluationDependsOn(':launcher')
void addLauncherDist(Distribution dist) {
    if(!project(':launcher').linuxX64DistZip.enabled) {
        println("WARNING: Linux x64 launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').linuxX64DistZip) {
                into 'launcher'
            }
        }
    }
    if(!project(':launcher').linuxAarch64DistZip.enabled) {
        println("WARNING: Linux aarch64 launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').linuxAarch64DistZip) {
                into 'launcher'
            }
        }
    }
    if(!project(':launcher').winX64DistZip.enabled) {
        println("WARNING: Windows x64 launcher dist not enabled, not included in BDeploy release ZIPs");
    } else {
        dist.contents {
            from(project(':launcher').winX64DistZip) {
                into 'launcher'
            }
        }
    }
}

distributions {
    winX64 {
        distributionBaseName = 'bdeploy-win-x64'
        addLauncherDist(it)
        contents {
            exclude '_bdeploy'
        }
    }
    linuxX64 {
        distributionBaseName = 'bdeploy-linux-x64'
        addLauncherDist(it)
        contents {
            exclude '_bdeploy.bat'
            rename('_bdeploy', 'bdeploy')
        }
    }
    linuxAarch64 {
        distributionBaseName = 'bdeploy-linux-aarch64'
        addLauncherDist(it)
        contents {
            exclude '_bdeploy.bat'
            rename('_bdeploy', 'bdeploy')
        }
    }
}


